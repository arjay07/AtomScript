# AtomScript Notepad #

include <io>;
include <gui>;
include <java.awt.Font>;
include <javax.swing.JScrollPane>;

@mainFrame;
@label;
@scroll;
@textArea;
@openButton;
@saveButton;
@newButton;
@currentFile;

$main(){

	print("Loading window...");
	createWindow();
	showMainWindow();    

}

$createWindow(){

	@font = *Font("Consolas", Font<MONOSPACED>, 14);

	label = gui<newLabel>("Notepad - new");
	textArea = gui<newTextArea>(1920, 1080, font, true);
	newButton = gui<newButton>("New File", newFile);
	saveButton = gui<newButton>("Save File", openSaveFile);
	openButton = gui<newButton>("Open File", openOpenFile);
	
	@scroll = *JScrollPane(textArea);
	
	mainFrame = gui<newWindow>("AtomScript Simple Notepad", 520, 360, [
	
		label,
		scroll,
		newButton,
		openButton,
		saveButton
	
	]);
	
	#mainFrame<setResizable>(false);

}

$showMainWindow(){

	mainFrame<setVisible>(true);

}

$openSaveFile(){
	
	@fileName = label<getText>();
	@fileContent = textArea<getText>();
	
	if(fileName.length > 0){
	
		@saved = IO<saveFile>(mainFrame, fileContent);
		if(saved){
		
			gui<alert>("Save successful!");
		
		}else{
		
			gui<alert>("Whoops! Something went wrong!");
		
		}
		
	}else{
	
		gui<alert>("Please enter a file name...");
	
	}

}

$openOpenFile(){

	@file = IO<openFile>(mainFrame);
	@read = IO<readFile>(file);
	textArea<setText>(read);
	label<setText>("Notepad - " + file<getName>());

}

$newFile(){

	label<setText>("Notepad - new");
	textArea<setText>("");

}